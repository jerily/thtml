#set dir [file dirname [info script]]
#set auto_path [linsert $auto_path 0 [file join $dir ..]]

package require tcltest
package require thtml

namespace import -force ::tcltest::test

::tcltest::configure {*}$argv

proc escape {str} {
    return [string map {\r {\r} \n {\n}} $str]
}

test if-1 {} -body {
    set data {
        title "Hello, World!"
        age 47
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl if="$age > 21">
                    <p>You are an adult.</p>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
    escape $html
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1><p>You are an adult.</p></body></html>}


test if-2 {} -body {
    set data {
        title "Hello, World!"
        name "John Smith"
        age 47
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl if="$age > 21 && $name eq {John Smith}">
                    <p>You are an adult.</p>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
    escape $html
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1><p>You are an adult.</p></body></html>}

test if-3 {} -body {
    set data {
        title "Hello, World!"
        name "John Smith"
        age 47
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl if='$age > 21 && $name eq "John Smith"'>
                    <p>You are an adult.</p>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
    escape $html
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1><p>You are an adult.</p></body></html>}

test transform-1 {} -body {
    set compiled_template [::thtml::compiler::tcl_transform "\x02hello world\x03puts hey\x02this is a test\x03"]
    escape $compiled_template
} -result {\nappend ds "hello world"\nputs hey\nappend ds "this is a test"\n}

test var-substitution-1 {} -body {
    set data {
        title "Hello, World!"
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1></body></html>}

test foreach-within-if-1 {} -body {
    set data {
        title "Hello, World!"
        age 47
        items {a b c d e}
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl if="${age} > 21">
                    <p>You are an adult.</p>
                    <tpl foreach="item" in="${items}">
                        <p>@{item}: @{age}</p>
                    </tpl>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1><p>You are an adult.</p><p>a: 47</p><p>b: 47</p><p>c: 47</p><p>d: 47</p><p>e: 47</p></body></html>}


test nested-if-in-foreach-1 {} -body {
    set data {
        title "Hello, World!"
        items {1 2 3 4 5}
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl foreach="item" in="${items}">
                    <tpl if="${item} % 2 == 0"><p>@{item} is even</p></tpl>
                    <tpl if="${item} % 2 == 1"><p>@{item} is odd</p></tpl>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1><p>1 is odd</p><p>2 is even</p><p>3 is odd</p><p>4 is even</p><p>5 is odd</p></body></html>}

test nested-foreach-1 {} -body {
    set data {
        title "Hello, World!"
        items {{1 2} {3 4} {5 6}}
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl foreach="item" in="${items}">
                    <tpl foreach="subitem" in="${item}">
                        <p>@{subitem} is <tpl if="${subitem} % 2 == 0">even</tpl><tpl if="${subitem} % 2 == 1">odd</tpl></p>
                    </tpl>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1><p>1 is odd</p><p>2 is even</p><p>3 is odd</p><p>4 is even</p><p>5 is odd</p><p>6 is even</p></body></html>}

test include-1 {} -body {
    set data {
        title "Hello, World!"
        name "John Smith"
        age 47
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl include="./templates/name_and_age.thtml" name="${name}" age="${age} ye\[a\]rs">
                    <tpl if="${age} > 21">
                        You are an adult.
                    </tpl>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
    escape $html
} -result {<html><head><title>Hello, World!</title></head><body><h1>Hello, World!</h1><div><h2>Name and age</h2>\n    John Smith is 47 ye[a]rs old.\n    \n                        You are an adult.\n                    <div>This is the footer</div>.\n</div></body></html>}

test include-2-circular-dependency {} -body {
    set data {
        title "Hello, World!"
        name "John Smith"
        age 47
    }
    set template {
        <html>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <tpl include="./templates/name_and_age.thtml" name="${name}" age="${age} ye\[a\]rs">
                    <tpl if="${age} > 21">
                        You are an adult.
                        <tpl include="./templates/name_and_age.thtml" name="${name}" age="${age} ye\[a\]rs" />
                    </tpl>
                </tpl>
            </body>
        </html>
    }
    set html [::thtml::render $template $data]
    escape $html
} -returnCodes error -result {circular dependency detected}

test eval-1 {} -body {
    set template {
        <html>
            <tpl val="x">return @{loggedin}</tpl>
            <tpl val="y">return 2</tpl>
            <tpl val="z">return [expr { @{x} + @{y} }]</tpl>
            <head>
                <title>@{title}</title>
            </head>
            <body>
                <h1>@{title}</h1>
                <p>@{z}</p>
            </body>
        </html>
    }
    ::thtml::render $template {title "You rock!" loggedin 1}
} -result {<html><head><title>You rock!</title></head><body><h1>You rock!</h1><p>3</p></body></html>}